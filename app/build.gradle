apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.teso.net"
        multiDexEnabled true
        minSdkVersion 22
        targetSdkVersion 27
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'API_BASE_URL', '"http://playground.tesonet.lt"'
        }

        debug {
            buildConfigField 'String', 'API_BASE_URL', '"http://playground.tesonet.lt"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven {
        url "https://s3.amazonaws.com/repo.commonsware.com"
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    def support_version = '27.1.0'
    implementation "com.android.support:appcompat-v7:${support_version}"
    implementation "com.android.support:recyclerview-v7:${support_version}"
    implementation "com.android.support:design:${support_version}"

    def stetho_version = '1.5.0'
    implementation "com.facebook.stetho:stetho:${stetho_version}"
    implementation "com.facebook.stetho:stetho-okhttp3:${stetho_version}"
    implementation "com.facebook.fresco:fresco:1.8.1"

    def retrofit_version = '2.3.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    def ok_http_version = '3.8.1'
    implementation "com.squareup.okhttp3:logging-interceptor:$ok_http_version"

    def leakcanary_version = '1.5.4'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

    def dagger_version = '2.5'
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    def arch_version = '1.1.1'
    implementation "android.arch.lifecycle:runtime:$arch_version"
    implementation "android.arch.lifecycle:extensions:$arch_version"
    implementation "android.arch.lifecycle:reactivestreams:$arch_version"
    kapt "android.arch.lifecycle:compiler:$arch_version"

    def room_version = '1.1.0'
    implementation "android.arch.persistence.room:runtime:$room_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"
    implementation "com.commonsware.cwac:saferoom:0.3.2"
    implementation "net.zetetic:android-database-sqlcipher:3.5.9@aar"

    implementation 'com.android.support.constraint:constraint-layout:1.1.1'
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex:rxkotlin:1.0.0'
    implementation 'com.scottyab:secure-preferences-lib:0.1.4'
    implementation "org.jetbrains.anko:anko-commons:0.10.4"
    implementation 'jp.wasabeef:fresco-processors:2.1.0'
    implementation 'com.budiyev.android:circular-progress-bar:1.1.9'
}
